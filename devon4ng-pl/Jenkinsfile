@Library('ProductionLineTemplateLib')

import com.capgemini.productionline.configuration.*

JenkinsConfiguration jenkinsConfiguration = new JenkinsConfiguration(this);
GitLab gitlabConfiguration = new GitLab(this, params.GITLAB_USER_PRIVATE_TOKEN, ProductionLineGlobals.GITLAB_BASE_URL);

 

pipeline{

    agent any

    tools {
        nodejs "NodeJS LTS"
    }
    //Jenkins Job Parameters
    parameters { 
        string(name: 'PROJECT_NAME', defaultValue: 'devonfw', description: 'Name of the project. The word "-frontend" will be appended.') 
        string(name: 'GROUP_ID', defaultValue: 'com.devonfw', description: 'The group id of the project.') 
        string(name: 'GITLAB_USER_PRIVATE_TOKEN', defaultValue: 'wzsJ2ovaaspLZDe8_eL8', description: 'NEEDS TO BE SET!. Private Token of a Production Line Gitlab User that can be used to create repositories.') 
        string(name: 'GITLAB_CREATE_GROUP_NAME', defaultValue: 'devon', description: 'Name of the group that will be created inside GitLab to clone the repo.') 
        string(name: 'GITLAB_CREATE_BRANCH', defaultValue: 'master', description: 'Branch') 
        string(name: 'GITLAB_CREATE_PROJECT_DESCRIPTION', defaultValue: 'Devon for angular PL template', description: 'Description of the repository.')
        credentials(credentialType: 'com.cloudbees.plugins.credentials.impl.UsernamePasswordCredentialsImpl', defaultValue: 'gitlab-user', description: 'Gitlab integration user credentials', name: 'GITLAB_CREDENTIALS', required: true)
        choice choices: ['none', 'docker', 'openshift'], description: 'Choose the environment where you want to deploy', name: 'DEPLOY'
        
    }
    
    stages {
        stage('PL setup') {
            steps{
                script{
                    PROJECT_NAME_FULL = params.PROJECT_NAME + "-frontend"                

                    println "Trying to install all required plugins"
                    //Install the required Jenkins plugins
                    //The template currently requires the following plugins to be present
                    pluginsToInstall = ["http_request", "job-dsl", "pipeline-maven", "NodeJS+Plugin"]

                    // To deploy at docker, some extra plugins are required
                    if (params.DEPLOY == "docker" || params.DEPLOY == "openshift"){
                        pluginsToInstall << "Docker+Plugin" << "Docker+build+step+plugin" << "Docker+Pipeline+Plugin" << "JClouds+Plugin"
                    }

                    if (params.DEPLOY == "openshift") {
                        pluginsToInstall << "OpenShift+Client+Plugin"
                    }

                    def pluginsHaveBeenInstalled = jenkinsConfiguration.installPlugin(pluginsToInstall)
                    
                    if( pluginsHaveBeenInstalled ) {
                        println "New plugins were installed - Restart"
                        // We need to restart Jenkins in case new plugins have been installed.
                        //if set to false, then do a force restart
                        //still requires to manually start the job again afterwards
                        jenkinsConfiguration.restartJenkins(false);
                    } else {
                        println "No plugins were installed"
                    }

                    //Add nodejs
                    println "Trying to install nodejs config"
                    String installName = "NodeJS 10.16.0"
                    String nodeJS_Version = "10.16.0"
                    String npmPackages = "yarn"
                    String home = ""
                    long npmPackagesRefreshHours = 100

                    nodeJSInstallSuccess = jenkinsConfiguration.addNodeJS_Version(installName, nodeJS_Version, npmPackages, home, npmPackagesRefreshHours)
                    

                    if (params.DEPLOY == "openshift"){
                        println "Trying to install docker as custom tool"
                        
                        jenkinsConfiguration.addCustomTool("docker-global", "", """if ! which docker > /dev/null; then
    sudo apt-get update
    sudo apt-get install -y apt-transport-https ca-certificates curl gnupg2 software-properties-common
    curl -fsSL https://download.docker.com/linux/debian/gpg | sudo apt-key add -
    sudo add-apt-repository "deb [arch=amd64] https://download.docker.com/linux/debian \$(lsb_release -cs) stable"
    sudo apt-get update
    sudo apt-get install -y docker-ce-cli
    docker -v
fi""", "/usr/bin")
                    }

                    if (params.DEPLOY == "openshift") {
                        println "openshift"
                        jenkinsConfiguration.installOpenshift("OpenShiftv3.11.0", "", """
if [ ! -f /opt/oc3.11.0/oc ]; then
  echo "oc3.11.0 does not exist."
  wget https://github.com/openshift/origin/releases/download/v3.11.0/openshift-origin-client-tools-v3.11.0-0cbc58b-linux-64bit.tar.gz
  tar -xvzf openshift-origin-client-tools-v3.11.0-0cbc58b-linux-64bit.tar.gz
  sudo mv openshift-origin-client-tools-v3.11.0-0cbc58b-linux-64bit /opt/oc3.11.0
else
  echo "oc3.11.0 exist."
fi
                        """, "/opt/oc3.11.0")
                    }

                    // Get the Pl Url
                    env.PL_URL = JENKINS_URL.replace("https://", "").split("/")[0]
                    
                    //Add the required script approval
                    println "Adding required script approvals in Jenkins"
                    jenkinsConfiguration.approveSignature("method groovy.lang.GroovyObject invokeMethod java.lang.String java.lang.Object")
                }
            }
        }

        stage('Configuring docker'){
            when {
                beforeInput true
                equals expected: null, actual: env.DOCKER_HOST
                equals expected: 'docker', actual: params.DEPLOY
            }
            input {
                message 'Introduce the docker host'
                parameters {
                    string defaultValue: 'tcp://127.0.0.1:2375', description: 'Remote docker url', name: 'DOCKER_HOST', trim: true
                }
            }
            steps {
                script {
                    jenkinsConfiguration.addJenkinsGlobalEnvironmentVariable("DOCKER_HOST", DOCKER_HOST)

                    tool 'docker-global'

                    def network = sh returnStdout: true, script: """docker network ls -f name=application | sed '2q;d' """
                    if (network?.trim() == '') {
                        sh "docker network create application"
                    } else {
                        println "application network already exists"
                    }
                }
            }
        }
        
        stage('Configuring Openshift'){
            when {
                beforeInput true
                equals expected: null, actual: env.DOCKER_HOST
                equals expected: 'openshift', actual: params.DEPLOY
            }
            input {
                message 'Introduce the docker host'
                parameters {
                    string defaultValue: 'default', description: 'Openshift configuration name', name: 'OC_NAME', trim: true
                    string defaultValue: 'https://ocp.itaas.s2-eu.capgemini.com', description: 'Openshift url', name: 'OC_URL', trim: true
                    string defaultValue: 's2portaldev', description: 'Openshift project', name: 'OC_PROJECT', trim: true
                    credentials credentialType: 'com.openshift.jenkins.plugins.OpenShiftTokenCredentials', description: 'The openshift auth token. You can get it with the command: oc whoami -t', name: 'OC_TOKEN', required: true
                }
            }
            tools {
                oc 'OpenShiftv3.11.0'
            }
            steps {
                script {
                    jenkinsConfiguration.addOpenshiftGlobalConfiguration(OC_NAME, OC_URL, OC_TOKEN, OC_PROJECT)

                    env.OC_URL = OC_URL
                    env.OC_PROJECT = OC_PROJECT

                    def template = httpRequest consoleLogResponseBody: true, httpMode: 'GET', url: "http://gitlab-core:80/gitlab/darrodri/devonfw-shop-floor/raw/feature/devon4ng-template/dsf4openshift/openshift-devonfw-deployment/templates/devon4ng.json"

                    sh """echo '${template.getContent()}' > template.json """
                    sh """chmod 777 template.json """

                    openshift.withCluster('default'){
                        openshift.withProject(env.OC_PROJECT) {
                            openshift.create( openshift.process( '', '-f', 'template.json', "-p", "APPLICATION_NAME=${PROJECT_NAME}", "-p", "DOCKER_IMAGE=docker-registry-${env.PL_URL}/${PROJECT_NAME}", "-p", "DOCKER_TAG=latest", "-p", "CPU_LIMIT=200m", "-p", "MEMORY_LIMIT=500Mi"  ) )
                        }
                    }
                }
            }
        }
        
        stage('Create Devon4ng repo') {
            steps{
                script{
                    println "Trying to create gitlab group"
                    //Check if group already exists
                    if(gitlabConfiguration.getGroupId(params.GITLAB_CREATE_GROUP_NAME) == ""){
                        println "No group existing yet, trying to create"
                        //Create a group for devon
                        // public createGroup(String groupname, String grouppath, String groupdesc, String grouptype) {
                        gitlabConfiguration.createGroup(params.GITLAB_CREATE_GROUP_NAME, params.GITLAB_CREATE_GROUP_NAME, params.GITLAB_CREATE_PROJECT_DESCRIPTION, "public")
                    } else {
                        println "Group already exists"
                    }
                    //Check if project already exists
                    println "Trying co create Gitlab project"
                    if(gitlabConfiguration.getProjectId(params.GITLAB_CREATE_GROUP_NAME, PROJECT_NAME_FULL) == ""){
                        println "No project with the same name exists yet, trying to create"
                        //Create a new public repository for Devon4ng in the Production Line
                        gitlabConfiguration.createProject(
                            params.GITLAB_CREATE_GROUP_NAME, 
                            PROJECT_NAME_FULL, 
                            PROJECT_NAME_FULL, 
                            params.GITLAB_CREATE_PROJECT_DESCRIPTION, 
                            params.GITLAB_CREATE_BRANCH, 
                            "public"
                        )
                    } else {
                        println "Project with the same name already exists"
                    }
                    gitlabConfiguration.createWebhook(params.GITLAB_CREATE_GROUP_NAME, PROJECT_NAME_FULL, "http://jenkins-core:8080/jenkins/project/${PROJECT_NAME}/${PROJECT_NAME_FULL}", '')
                }
            }
        }

        stage('Generate Angular template job'){
            steps{
                println "Trying to create Jenkins jobs"
                script{
                    //Disable Job Security
                    println "Disable job dsl security"
                    jenkinsConfiguration.disableJobDSLScriptSecurity()

                    //Prepare folders in Jenkins for the jobs
                    println "Trying to create folder for jobs"
                    jobDsl scriptText: """
                        folder("${PROJECT_NAME}"){
                            description('Jobs for ${PROJECT_NAME} project.')
                        }
                        """

                    //Build job
                    println "Trying to create Build job"
                    jobDsl scriptText: """
                        multibranchPipelineJob("${PROJECT_NAME}/${PROJECT_NAME_FULL}") {
                            description('Build job for devon4ng template.')
                            branchSources {
                                git {
                                    remote('${ProductionLineGlobals.GITLAB_BASE_URL}/${params.GITLAB_CREATE_GROUP_NAME}/${PROJECT_NAME_FULL}.git')
                                    credentialsId('github-ci')
                                    includes('master release* develop')
                                }
                            }
                            orphanedItemStrategy {
                                discardOldItems {
                                    numToKeep(10)
                                }
                            }
                        }
                        """

                    //Enable Job Security again
                    println "Enable job dsl security"
                    jenkinsConfiguration.enableJobDSLScriptSecurity()
                }
            }
        }
    
        stage('Install angular & cicdgen') {
            steps{
                cleanWs()
                deleteDir()

                sh 'npm i -g @angular/cli @devonfw/cicdgen'
            }
        }
      
        stage('ng new') {
            steps{
                sh "ng new ${PROJECT_NAME_FULL} --routing --defaults --skip-install --style scss"
            }
        }
        
        stage('Create cicdgen Angular template') {
            steps{
                script{
                    PL_NAME = sh(returnStdout: true, script: "curl -v -H 'Content-Type: application/json' -H 'Authorization: $MICROPORTAL_TOKEN' 'http://microportal:8080/api/metadata' | jq -r '.engagementname'")   
                    PL_NAME = PL_NAME.trim()   
                    
                    def dockerString = ''
                    def ocString = ''

                    if (params.DEPLOY == 'docker'){
                        dockerString = "--docker --plurl ${PL_URL} "
                    }

                    if (params.DEPLOY == 'openshift'){
                        ocString = "--openshift --ocurl ${env.OC_URL} --ocn ${env.OC_PROJECT} --plurl ${env.PL_URL} "
                    }
                
                    dir(PROJECT_NAME_FULL) {
                        sh 'git config user.email "jenkins-pl@capgemini.com"'
                        sh 'git config user.name "Jenkins"'
                        sh "cicdgen generate devon4ng ${ocString}${dockerString}--groupid ${params.GROUP_ID}"
                    }
                }
            }
        }

        stage('Pushing template to Gitlab') {
            steps{
                script {
                    dir(PROJECT_NAME_FULL){
                        withCredentials([usernamePassword(credentialsId: params.GITLAB_CREDENTIALS, passwordVariable: 'gitlab_password', usernameVariable: 'gitlab_user')]) {
                            sh "git status && git remote add origin http://${gitlab_user}:${gitlab_password}@gitlab-core:80/gitlab/${GITLAB_CREATE_GROUP_NAME}/${PROJECT_NAME_FULL}.git && git push --set-upstream origin master"
                        }
                    }    
                }
            }
        }
    }
}
